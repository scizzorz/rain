link "string.c"

import core
import iter

export to_int = foreign "rain_ext_str_to_int"(val)
export fmt    = foreign "rain_ext_fmt"(val, fmt)

export substring = func(s, start, end)
  let len = length(s)
  if start >= end | end > len
    return ""
  return s[start] $ substring(s, start+1, end)

export index_of = func(s, of)
  for i in iter.range(length(s))
    if s[i] == of
      return i

export split = func(s, at)
  let arr = table
  let arr_idx = 0
  let recurse = func(s)
    if s == null | s == ""
      return
    let i = index_of(s, at)
    if i != null
      let cur = substring(s, 0, i)
      let rest = substring(s, i+1, length(s))
      arr[arr_idx] = cur
      arr_idx = arr_idx + 1
      recurse(rest)
    else
      arr[arr_idx] = s
  recurse(s)
  return arr
