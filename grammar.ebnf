program :: (stmt NEWLINE)+ EOF

block :: INDENT (stmt NEWLINE)+ DEDENT

stmt :: 'var' var_prefix ('=' compound)?
      | 'bind' NAME (',' NAME)*
      | 'foreign' (NAME | STRING) '=' NAME
      | 'import' (NAME '=')? import_mod
      | 'macro' NAME fnparams 'as' fnparams block
      | macro_exp
      | 'link' STRING
      | 'library' STRING
      | if_stmt
      | 'for' var_prefix 'in' binexpr block
      | 'with' binexpr ('as' NAME (',' NAME)*)
      | 'while' binexpr block
      | 'until' binexpr block
      | 'loop' block
      | 'pass'
      | 'break' ('if' binexpr)?
      | 'continue' ('if' binexpr)?
      | 'return' compound?
      | 'save' (NAME '=')? compound
      | assn_prefix ('=' compound | fnargs | ':' NAME  fnargs)

import_mod :: ('.' '/')? (NAME | STRING) ('/' (NAME | STRING))*

if_stmt :: 'if' binexpr block (NEWLINE 'else' (if_stmt | block))?

macro_exp :: '@' NAME ('.' NAME)* ***

var_prefix :: '[' var_prefix (',' var_prefix)* ']'
            | NAME

assn_prefix :: '[' assn_prefix (',' assn_prefix)* ']'
             | prefix ('.' NAME | '[' binexpr ']')*

array_expr :: '[' (binexpr (',' binexpr)*)? ','? ']'

dict_item :: ((NAME | '[' binexpr ']') '=' binexpr)

dict_expr :: '{' (dict_item (',' dict_item)*)? ','? '}'

fnargs :: '(' (binexpr (',' binexpr)*)? ')'

fnargblock :: INDENT (compound NEWLINE)+ DEDENT

fnparams :: '(' (NAME (',' NAME)*)? ')'

compound :: macro_exp
          | 'func' (NAME | STRING)? fnparams ('->' binexpr | block)
          | 'catch' block
          | binexpr

binexpr :: unexpr (OPERATOR unexpr)*

unexpr :: ('-' | '!') simple
        | simple

simple :: 'func' fnparams '->' binexpr
        | 'foreign' (NAME | STRING) fnparams
        | array_expr
        | dict_expr
        | primary

primary :: prefix ('?'? fnargs | ':' NAME '?'? fnargs | '.' NAME | '[' binexpr ']')*

prefix :: '(' binexpr ')'
        | NAME | INT | FLOAT | BOOL | STRING | NULL | (TABLE dict_expr?)
