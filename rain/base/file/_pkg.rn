link "file.c"

var fopen = foreign "rain_ext_fopen"(name, mode)
var fread = foreign "rain_ext_fread"(handle, size)
var freadline = foreign "rain_ext_freadline"(handle)
var fwriteline = foreign "rain_ext_fwriteline"(handle, line)
var fclose = foreign "rain_ext_fclose"(handle)

export file_not_found = table :: except.error
file_not_found.msg = "cannot open file"
foreign "rain_exc_file_not_found" = file_not_found

var base = table
base.new = func(self, handle)
  save ret = table :: self

  ret["_file"] = handle

base.readline = func(self)
  return freadline(self["_file"])

base.read = func(self, size)
  return fread(self["_file"], size)

base.writeline = func(self, line)
  fwriteline(self["_file"], line)

base.close = func(self)
  fclose(self["_file"])

base.lines = func(self)
  return func()
    bind self
    return self:readline()

export open = func(name, mode)
  return base:new(fopen(name, mode))
