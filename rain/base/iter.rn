export count_by = func(step)
  var next = 0
  return func()
    save next
    next = next + step

export count = func() -> count_by(1)

export srange_by = func(start, end, step)
  var next = start
  return func()
    if next < end
      save next
    next = next + step

export srange = func(start, end) -> srange_by(start, end, 1)

export range = func(end) -> srange_by(0, end, 1)

export map = func(iter, fn)
  return func()
    var val = iter()
    if val != null
      return fn(val)

export foldl = func(iter, fn, z)
  var a = z
  for v in iter
    a = fn(a, v)
  return a

export enum = func(iter)
  var i = 0
  return func()
    var next = iter()
    if next == null
      return

    save [i, next]
    i = i + 1

export zip = func(one, two)
  return func()
    var n1 = one()
    var n2 = two()
    if n1 == null | n2 == null
      return

    return [n1, n2]

export zip_with = func(one, two, fn)
  return func()
    var n1 = one()
    var n2 = two()
    if n1 == null | n2 == null
      return

    return fn(n1, n2)

export take = func(iter, end)
  var next = 0
  return func()
    if next < end
      save iter()
    next = next + 1

export drop = func(iter, num)
  var i = 0
  while i < num
    iter()
    i = i + 1

  return iter

export repeat = func(val)
  return func()
    return val

export replicate = func(times, val)
  return func()
    if times > 0
      times = times - 1
      return val
