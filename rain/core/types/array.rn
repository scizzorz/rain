link "./array.c"

var to_str = foreign "rain_ext_to_str"(val)
var meta = foreign "rain_ext_meta"(val)
export length = foreign "rain_ext_array_length"(val)

export values = func(self)
  var i = 0
  return func()
    save self[i]
    i = i + 1

export keys = func(self)
  var i = 0
  return func()
    if self[i] == null
      return

    save i
    i = i + 1

export items = func(self)
  var i = 0
  return func()
    if self[i] == null
      return null

    save [i, self[i]]
    i = i + 1

export map = func(self, fn)
  save ret = table :: meta(self)
  for i in keys(self)
    ret[i] = fn(self[i])

export foldl = func(self, fn, z)
  var a = z
  for v in values(self)
    a = fn(a, v)
  return a

export concat = func(self, other)
  save ret = table :: meta(self)
  for i in keys(self)
    ret[i] = self[i]
  var len = length(self)
  for i in keys(other)
    ret[i+len] = other[i]

export flatmap = func(self, fn)
  var mapped = map(self, fn)
  return foldl(mapped, concat, table :: meta(self))

export contains = func(self, value)
  save false
  for v in values(self)
    if v == value
      return true

export from_iter = func(iter)
  save ret = table
  var idx = 0
  for i in iter
    ret[idx] = i
    idx = idx + 1

export slice = func(self, start, end)
  save ret = table
  var to_idx = 0
  var from_idx = start
  while from_idx < end
    ret[to_idx] = self[from_idx]
    from_idx = from_idx + 1
    to_idx = to_idx + 1

export str = func(self)
  var ret = "["
  var i = 0
  while self[i] != null
    ret = ret $ tostr(self[i])
    i = i + 1
    if self[i] != null
      ret = ret $ ", "

  ret = ret $ "]"
  return ret

export from_iter = func(iter)
  var i = 0
  save ret = []

  loop
    ret[i] = iter()
    break if ret[i] == null
    i = i + 1
